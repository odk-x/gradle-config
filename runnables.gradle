/**************************************************************************************************
                                        GRADLE RUNNABLES
 *************************************************************************************************/

/*
*   @author: marshallbradley93@gmail.com
*
*   This Gradle script contains common build logic in the form of runnables (both tasks and methods).
*   These methods are dispatched to the relevant project files via either local or remote.gradle.
*
*   @TODO: Convert to custom Gradle plugin
*/
 
/**************************************************************************************************
                                            TASKS
 *************************************************************************************************/


 // Define the snapshot version to be the git hash
task generateSnapshotVersion {
    def gitHash = new ByteArrayOutputStream()
    def date = (new Date()).format('yyyyMMddHHmm')
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = gitHash
    }
    doLast {
        // Package the snapshot version into an external object
        project.ext.snapshotVersionCode = 'b' + date + '_v' + gitHash.toString().trim()
    }
}

// Set the version to release (required by Artifactory)
task releaseVersion {
	doLast {
		version = releaseVersionCode
	}
}

// Set the version to snapshot (required by Artifactory)
task snapshotVersion(dependsOn: "generateSnapshotVersion") {
	doLast {
		// snapshotVersionCode is set by a doLast in generateSnapshotVersion code
		version = snapshotVersionCode
	}
}

// copy the analytics files (if present)
task analytics {
	doFirst {
		def projDirectory = new File("${project.projectDir}");
		def parentProjDir = projDirectory.getParentFile();
		def dirLoc = parentProjDir.getAbsolutePath() + "/odkanalytics"
		println dirLoc
		if(file(dirLoc).exists() ) {
			new File(dirLoc).eachFile() { 
				file -> println file.absolutePath
					def newFile = "${project.projectDir}/" + file.getName() 
					println newFile 
					new File(newFile).text = file.text
				 
			}
		} else {
			def fakePropertiesFile = "${project.projectDir}/fabric.properties"
			if(!(file(fakePropertiesFile).exists())) {
				new File(fakePropertiesFile) << "apiKey=testkey"
			}
		}	
	}
}

tasks.whenTaskAdded { task ->
    if (task.name.startsWith('compile')) {
        task.dependsOn analytics
    }
}
